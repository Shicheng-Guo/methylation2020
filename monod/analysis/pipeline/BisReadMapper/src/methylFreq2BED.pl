#!/usr/bin/perl -w
use strict;
# A perl script for converting a methylFreq file to a UCSC BED file. 
# USAGE: ./methylFreq2BED.pl sampleID [minDepth] < sample_methylFreq.txt
#   sample_methylFreq.txt: the methylFreq file generated by bisReadMapper.pl
# Format of the BED file:
# Column 1: chromosome
# Column 2: chromosome_start
# Column 3: chromosome_end
# Column 4: methylation level [0-1]
# column 5: sequencing depth
# column 6: strand, please ignored since the reads from both strands were combined
# Column 7: chromosome_start
# Column 8: chromosome_end
# Column 9: pesudo color, red is methylated, and green is unmethylated
#
# Written by Kun Zhang (kzhang@bioeng.ucsd.edu), last modified 10/07/2010 
#

my $sampleID = $ARGV[0];
$sampleID = "Sample" if (!$sampleID);
my $minDepth = $ARGV[1];
$minDepth = 10 if(!$minDepth);

my @palette=("0,240,0", "30,210,0","60,180,0","90,150,0","120,120,0","150,90,0","180,60,0","210,0,0");

my %methylTable;

sub main(){
	while(my $line = <STDIN>){
		chomp($line);
		my @fields = split(/\t/, $line);		
		my $strand = $fields[2] eq 'W' ? '+' : '-';
		my %alleleCounts;
		my $CT_counts;
		for(my $i=5; $i<scalar(@fields); $i+=2){
			$alleleCounts{$fields[$i]}=$fields[$i+1];			
			$CT_counts += $fields[$i+1] if($fields[$i]=~ /[CT]/);
		}
		next if(!$CT_counts || $CT_counts/$fields[3] < 0.9);
		my $index=$fields[0] . ":" . $fields[1];
		$alleleCounts{'C'} =0 if(!$alleleCounts{'C'});
		$methylTable{$index}->{'C'} +=  $alleleCounts{'C'} ;
		$methylTable{$index}->{'CT'} += $CT_counts;
	}
	report_methylFreqBED();
}

sub report_methylFreqBED(){
	print "track name=\"", $sampleID, "\" description=\"$sampleID Methylation level\" visibility=2 useScore=1 itemRgb=\"On\"\n";
	foreach my $index(sort keys(%methylTable)){
		next if($methylTable{$index}->{'CT'}<$minDepth);
		my ($chr,$chr_pos) = split(/:/, $index);	
		my $methylLevel = sprintf("%4.3f", $methylTable{$index}->{'C'}/$methylTable{$index}->{'CT'});
		print "$chr\t", 
				$chr_pos-1, "\t", 
				$chr_pos, "\t",
				"\'", $methylTable{$index}->{'C'}, "/", $methylTable{$index}->{'CT'}, "\'\t",
				int($methylTable{$index}->{'CT'}), "\t+\t",
				$chr_pos-1, "\t",
				$chr_pos, "\t",
				$palette[int($methylLevel*8-0.0001)],"\n";	
	}
}

main();
